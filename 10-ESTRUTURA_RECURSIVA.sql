WITH ESTRUT(PAI, COMP, QTD, OPC, NIVEL, UM, PESO, PERCEN)
AS
(
SELECT	G1PAI.G1_COD							 AS [PAI], 
		G1PAI.G1_COMP							 AS [COMP], 
		(G1PAI.G1_QUANT * 1)					 AS [QTD],
		G1PAI.G1_OPC							 AS [OPC], 
		1										 AS [NIVEL],
		SB1PAICOMP.B1_UM						 AS [UM],
		SB1PAICOMP.B1_PESO						 AS [PESO],
		((SB1PAICOMP.B1_PESO * G1PAI.G1_QUANT)/ (SB1PAIPAI.B1_PESO * 1)) AS [PERCEN]
FROM SG1010 G1PAI
INNER JOIN SB1010 SB1PAICOMP ON ''			= SB1PAICOMP.B1_FILIAL 
						AND G1PAI.G1_COMP	= SB1PAICOMP.B1_COD
						AND ''				= SB1PAICOMP.D_E_L_E_T_
INNER JOIN SB1010 SB1PAIPAI ON ''			= SB1PAIPAI.B1_FILIAL 
						AND G1PAI.G1_COD	= SB1PAIPAI.B1_COD
						AND ''				= SB1PAIPAI.D_E_L_E_T_
WHERE G1PAI.G1_COD = '1150504401'
AND G1PAI.D_E_L_E_T_ = ''

UNION ALL

SELECT	EST.COMP									 AS [PAI], 
		G1FILHO.G1_COMP								 AS [COMP], 
		(EST.QTD * G1FILHO.G1_QUANT)				 AS [QTD], 
		G1FILHO.G1_OPC								 AS [OPC], 
		EST.NIVEL + 1								 AS [NIVEL],
		SB1FILHOCOMP.B1_UM							 AS [UM],
		SB1FILHOCOMP.B1_PESO						 AS [PESO],
		((EST.PERCEN) * ((SB1FILHOCOMP.B1_PESO * (EST.QTD * G1FILHO.G1_QUANT)) / (SB1FILHOPAI.B1_PESO * EST.QTD))) AS [PERCEN]
FROM SG1010 G1FILHO
INNER JOIN ESTRUT EST ON G1FILHO.G1_COD = EST.COMP
INNER JOIN SB1010 SB1FILHOCOMP ON ''		= SB1FILHOCOMP.B1_FILIAL 
						AND G1FILHO.G1_COMP	= SB1FILHOCOMP.B1_COD
						AND ''				= SB1FILHOCOMP.D_E_L_E_T_
INNER JOIN SB1010 SB1FILHOPAI ON ''			= SB1FILHOPAI.B1_FILIAL 
						AND G1FILHO.G1_COD	= SB1FILHOPAI.B1_COD
						AND ''				= SB1FILHOPAI.D_E_L_E_T_
WHERE G1FILHO.D_E_L_E_T_ = ''
)
SELECT PAI, COMP, QTD, OPC, NIVEL, UM, PESO, PERCEN FROM ESTRUT
GROUP BY PAI, COMP, QTD, OPC, NIVEL, UM, PESO, PERCEN
ORDER BY NIVEL, PAI, OPC, COMP
